plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT' apply false
	id 'java'
}

subprojects {
	apply plugin: 'java'

	java.toolchain.languageVersion = JavaLanguageVersion.of(17)

	jar {
		manifest {
			attributes([
					"Specification-Title"     : mod_name,
					"Specification-Vendor"    : mod_authors,
					"Specification-Version"   : project.jar.archiveVersion,
					"Implementation-Title"    : project.name,
					"Implementation-Version"  : project.jar.archiveVersion,
					"Implementation-Vendor"   : mod_authors,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"Timestampe"              : System.currentTimeMillis(),
					"Built-On-Java"           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
					"Build-On-Minecraft"      : minecraft_version
			])
		}
	}

	repositories {
		mavenCentral()
		// Add repositories to retrieve artifacts from in here.
		// You should only use this when depending on other mods because
		// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
		// See https://docs.gradle.org/current/userguide/declaring_repositories.html
		// for more information about repositories.

		maven {
			name = 'Sponge / Mixin'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"
		it.options.release = 17
	}

	tasks.withType(GenerateModuleMetadata) {
		enabled = false
	}
}
